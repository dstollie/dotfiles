# Shortcut to oh-my-zsh
export ZSH=$HOME/.oh-my-zsh

# Shortcut to dotfiles
export DOT=$HOME/.dotfiles

# Set default user to prevent name@host to show in the terminal
export DEFAULT_USER=`whoami`

# Set name of the theme to load. Optionally, if you set this to "random"
# it'll load a random theme each time that oh-my-zsh is loaded.
# See https://github.com/robbyrussell/oh-my-zsh/wiki/Themes
ZSH_THEME="agnoster"

source $ZSH/oh-my-zsh.sh

# User configuration
export PROJECTS=~/Code

# Stash your environment variables in ~/.localrc. This means they'll stay out
# of your main dotfiles repository (which may be public, like this one), but
# you'll have access to them in your scripts.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# all of our zsh files
typeset -U config_files
config_files=($DOT/**/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}
do
  source $file
done

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}
do
  source $file
done

# initialize autocomplete here, otherwise functions won't be loaded
# We do this once every 24 to speed things up.
# @see https://gist.github.com/ctechols/ca1035271ad134841284#gistcomment-1930109
setopt EXTENDEDGLOB
for dump in $HOME/.zcompdump(#qN.m1); do
  compinit
  if [[ -s "$dump" && (! -s "$dump.zwc" || "$dump" -nt "$dump.zwc") ]]; then
    zcompile "$dump"
  fi
done
unsetopt EXTENDEDGLOB
compinit -C

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}
do
  source $file
done

unset config_files

export PATH="$HOME/.yarn/bin:/Users/aron/Library/Python/2.7/bin:$HOME/.composer/vendor/bin:$PATH"

export LC_ALL=en_US.UTF-8 
export LANG=en_US.UTF-8
